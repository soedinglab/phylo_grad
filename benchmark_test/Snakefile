rule random_phylo:
    output: "data/random/root_tree_{num_leafs}.nwk"
    shell: "phylotree generate -t {wildcards.num_leafs} -b > {output}"

rule unroot:
    input: "data/random/root_tree_{num_leafs}.nwk"
    output: "data/random/tree_{num_leafs}.nwk"
    shell: "nw_reroot -d {input} > {output}"

rule random_fasta:
    input: "data/random/tree_{num_leafs}.nwk"
    output: "data/random/alignment_{num_leafs}_{L}.fasta"
    shell: "python random_fasta.py {input[0]} {wildcards.L} {output[0]}"

def benchmark_input(wildcards):
    num_leafs = [2**n for n in [4,6,8,10,11]]
    L = [300] * len(num_leafs)

    newick = [f"data/random/tree_{n}.nwk" for n in num_leafs]
    fasta = [f"data/random/alignment_{n}_{l}.fasta" for n,l in zip(num_leafs,L)]

    return newick + fasta

rule benchmark:
    input: files = benchmark_input, script = "benchmark.py"
    output: "data/random/time_{num_t}_{dtype}.txt"
    threads: lambda w : int(w.num_t)
    resources:
        slurm_extra="--exclusive",
        mem_mb=800000,
        runtime=1000
    shell: "OMP_NUM_THREADS={threads} RAYON_NUM_THREADS={threads} python benchmark.py {wildcards.dtype} pytorch {input.files} > {output}"

def benchmark_input_gpu(wildcards):
    num_leafs = [2**n for n in [4,6,8]]
    L = [300] * len(num_leafs)

    newick = [f"data/random/tree_{n}.nwk" for n in num_leafs]
    fasta = [f"data/random/alignment_{n}_{l}.fasta" for n,l in zip(num_leafs,L)]

    return newick + fasta
rule benchmark_gpu:
    input: files = benchmark_input_gpu, script = "benchmark.py"
    output: "data/random/time_gpu_{dtype}.txt"
    threads: 8
    shell: "OMP_NUM_THREADS=8 RAYON_NUM_THREADS=8 python benchmark.py {wildcards.dtype} pytorch_gpu {input.files} > {output}"

rule plot:
    input:  "plot.py",
            "data/random/time_{num_t}_{dtype}.txt"
    output: "data/random/plot_{num_t}_{dtype}_time.pdf",
            "data/random/plot_{num_t}_{dtype}_mem.pdf",
            "data/random/plot_{num_t}_{dtype}_ratio.pdf"
    shell: "python {input} {output}"